# Tiltfile.minimal for Nia Universal (Backend Only)
# Omits Interface, Dashboard, and Chorus TTS
#
# Prerequisites:
# 1. Install Tilt: https://docs.tilt.dev/install.html
# 2. Install a Kubernetes solutuion (k3d + docker engine, colima, etc.)
# 3. Start the kube cluster. Use a minimum of 2 CPUs and 4G memory.
# 4. Run: tilt up -f Tiltfile.minimal

reg = os.getenv("TILT_DEFAULT_REGISTRY", "")
ctx = k8s_context()

# Align image registry with the active cluster. Prefer explicit env, otherwise default
# to localhost:5000 for Colima so image pulls resolve without a stalled deploy phase.
if reg:
  default_registry(reg)
elif ctx and ctx.startswith("colima"):
  default_registry("localhost:5000")

allow_k8s_contexts([
  'colima-nia-dev',
  'k3d-nia-dev',
  'minikube',
  'kind-nia-dev',
  'docker-desktop',
  'microk8s'
])

# Limit concurrent updates to prevent OOM on local dev
update_settings(max_parallel_updates=3)

# Helper to load env file
def load_env(path):
    env = {}
    content = str(read_file(path, default=""))
    for line in content.splitlines():
        line = line.strip()
        if not line or line.startswith('#'):
            continue
        parts = line.split('=', 1)
        if len(parts) == 2:
            key = parts[0].strip()
            value = parts[1].strip()
            # Remove quotes if present
            if (value.startswith('"') and value.endswith('"')) or (value.startswith("'") and value.endswith("'")):
                value = value[1:-1]
            env[key] = value
    return env

# Load .env.local or .env
env = load_env('.env.local')
if not env:
    env = load_env('.env')

def get_env(key):
    val = env.get(key)
    if not val:
        fail("Missing required environment variable: " + key)
    return val

# --- 1. Infrastructure (Redis & Postgres) ---
k8s_yaml(blob("""
apiVersion: v1
kind: Service
metadata:
  name: redis
  labels:
    app: redis
spec:
  ports:
  - port: 6379
  selector:
    app: redis
---
apiVersion: apps/v1
kind: Deployment
metadata:
  name: redis
spec:
  strategy:
    type: RollingUpdate
    rollingUpdate:
      maxUnavailable: 0
      maxSurge: 1
  selector:
    matchLabels:
      app: redis
  template:
    metadata:
      labels:
        app: redis
    spec:
      containers:
      - name: redis
        image: redis:alpine
        ports:
        - containerPort: 6379
        command: ["redis-server", "--requirepass", "$(REDIS_PASSWORD)"]
        env:
        - name: REDIS_PASSWORD
          valueFrom:
            secretKeyRef:
              name: pipecat-daily-bot-stg-secret
              key: REDIS_SHARED_SECRET
---
apiVersion: v1
kind: Service
metadata:
  name: postgres-testdb
  labels:
    app: postgres-testdb
spec:
  ports:
  - port: 5432
  selector:
    app: postgres-testdb
---
apiVersion: apps/v1
kind: StatefulSet
metadata:
  name: postgres-testdb
spec:
  serviceName: "postgres-testdb"
  replicas: 1
  selector:
    matchLabels:
      app: postgres-testdb
  template:
    metadata:
      labels:
        app: postgres-testdb
    spec:
      containers:
      - name: postgres
        image: postgres:latest
        ports:
        - containerPort: 5432
        env:
        - name: POSTGRES_DB
          value: "testdb"
        - name: POSTGRES_USER
          value: "postgres"
        - name: POSTGRES_PASSWORD
          value: "password"
        - name: PGDATA
          value: "/var/lib/postgresql/data/pgdata"
        volumeMounts:
        - name: postgres-data
          mountPath: /var/lib/postgresql/data
  volumeClaimTemplates:
  - metadata:
      name: postgres-data
    spec:
      accessModes: [ "ReadWriteOnce" ]
      resources:
        requests:
          storage: 1Gi
"""))

k8s_resource('redis', port_forwards='6379:6379')
k8s_resource(
    'postgres-testdb',
    port_forwards='5432:5432'
)


# --- 2. Config & Secrets (Global) ---
k8s_yaml(blob("""
apiVersion: v1
kind: Secret
metadata:
  name: pipecat-daily-bot-stg-secret
stringData:
  BOT_CONTROL_SHARED_SECRET: "{BOT_CONTROL_SHARED_SECRET}"
  DAILY_API_KEY: "{DAILY_API_KEY}"
  DEEPGRAM_API_KEY: "{DEEPGRAM_API_KEY}"
  ELEVENLABS_API_KEY: "{ELEVENLABS_API_KEY}"
  ELEVENLABS_VOICE_ID: "{ELEVENLABS_VOICE_ID}"
  MESH_SHARED_SECRET: "{MESH_SHARED_SECRET}"
  OPENAI_API_KEY: "{OPENAI_API_KEY}"
  REDIS_SHARED_SECRET: "{REDIS_SHARED_SECRET}"
  YOUTUBE_API_KEY: "{YOUTUBE_API_KEY}"
  KOKORO_TTS_API_KEY: "test-key"
""".format(
  BOT_CONTROL_SHARED_SECRET=get_env('BOT_CONTROL_SHARED_SECRET'),
  DAILY_API_KEY=get_env('DAILY_API_KEY'),
  DEEPGRAM_API_KEY=get_env('DEEPGRAM_API_KEY'),
  ELEVENLABS_API_KEY=get_env('ELEVENLABS_API_KEY'),
  ELEVENLABS_VOICE_ID=get_env('ELEVENLABS_VOICE_ID'),
  MESH_SHARED_SECRET=get_env('MESH_SHARED_SECRET'),
  OPENAI_API_KEY=get_env('OPENAI_API_KEY'),
  REDIS_SHARED_SECRET=get_env('REDIS_SHARED_SECRET'),
  YOUTUBE_API_KEY=get_env('YOUTUBE_API_KEY'),
  BOT_TTS_PROVIDER=env.get('BOT_TTS_PROVIDER', 'kokoro')
)))
k8s_yaml(blob("""
apiVersion: v1
kind: ConfigMap
metadata:
  name: pipecat-daily-bot-stg-config
data:
  DAILY_API_URL: "https://api.daily.co/v1"
  DAILY_DOMAIN: "pearlos"
  DEBUG_BOT: "true"
  MESH_API_ENDPOINT: "http://mesh-stg/api"
  MESH_ENDPOINT: "http://mesh-stg/graphql"
  REDIS_AUTH_REQUIRED: "true"
  REDIS_URL: "redis://redis:6379"
  USE_REDIS: "true"
  BOT_RUNNER_MODE: "1"
  BOT_TTS_PROVIDER: "{BOT_TTS_PROVIDER}"
  KOKORO_TTS_BASE_URL: "ws://chorus-tts-kokoro-tts:80"
  KOKORO_TTS_VOICE_ID: "am_fenrir"
  KOKORO_TTS_SAMPLE_RATE: "24000"
"""))

k8s_yaml(blob("""
apiVersion: v1
kind: Secret
metadata:
  name: mesh-stg-secret
stringData:
  POSTGRES_PASSWORD: "{POSTGRES_PASSWORD}"
  MESH_SHARED_SECRET: "{MESH_SHARED_SECRET}"
  BOT_CONTROL_SHARED_SECRET: "{BOT_CONTROL_SHARED_SECRET}"
""".format(
  POSTGRES_PASSWORD=get_env('POSTGRES_PASSWORD'),
  MESH_SHARED_SECRET=get_env('MESH_SHARED_SECRET'),
  BOT_CONTROL_SHARED_SECRET=get_env('BOT_CONTROL_SHARED_SECRET')
)))

k8s_yaml(blob("""
apiVersion: v1
kind: ConfigMap
metadata:
  name: mesh-stg-config
data:
  POSTGRES_HOST: "postgres-testdb"
  POSTGRES_PORT: "5432"
  POSTGRES_USER: "postgres"
  POSTGRES_DB: "testdb"
  PORT: "2000"
"""))

k8s_resource(
    new_name='configs',
    objects=[
        'pipecat-daily-bot-stg-secret',
        'pipecat-daily-bot-stg-config',
        'mesh-stg-secret',
        'mesh-stg-config'
    ]
)

# Helper to patch YAMLs to use local images and remove namespace
def load_patched_yaml(path, new_image, replicas=None):
    # Replace ECR image with local image name
    # Also remove namespace lines so everything deploys to default namespace
    # We use sed to replace the specific ECR prefix to avoid false positives
    # And remove 'namespace: ...' lines (handling variable whitespace)
    # UPDATED: Replace the image URL anywhere (including env vars), not just in 'image:' field
    sed_cmd = 'sed -e "s|577124901432.dkr.ecr.us-east-2.amazonaws.com/[^[:space:]\\"]*|{}|g" -e "/^[[:space:]]*namespace:/d"'.format(new_image)

    if replicas:
        sed_cmd += ' -e "s|replicas: [0-9]*|replicas: {}|g"'.format(replicas)

    cmd = '{} {}'.format(sed_cmd, path)
    return local(cmd)

# --- 3. Pipecat Bot ---
docker_build(
    'nia-pipecat-bot',
    '.',
    dockerfile='apps/pipecat-daily-bot/Dockerfile',
    only=[
        'apps/pipecat-daily-bot',
        'packages/events',
        'packages/features'
    ],
    ignore=[
      '.git',
      '.codacy',
      '.codacy/**',
        '**/__pycache__',
        '**/.pytest_cache',
        '**/.venv',
        '**/venv',
        'apps/pipecat-daily-bot/.turbo',
        'apps/pipecat-daily-bot/bot/coverage',
        'apps/pipecat-daily-bot/bot/.coverage',
        'apps/pipecat-daily-bot/ui',
        'apps/pipecat-daily-bot/deployment',
        'packages/features/scripts',
    ]
)

k8s_yaml(load_patched_yaml('apps/pipecat-daily-bot/deployment/staging/02-service.yaml', 'nia-pipecat-bot'))
k8s_yaml(load_patched_yaml('apps/pipecat-daily-bot/deployment/staging/04-operator.yaml', 'nia-pipecat-bot'))
k8s_yaml(load_patched_yaml('apps/pipecat-daily-bot/deployment/staging/05-gateway.yaml', 'nia-pipecat-bot'))
k8s_yaml(load_patched_yaml('apps/pipecat-daily-bot/deployment/staging/07-warm-pool.yaml', 'nia-pipecat-bot', replicas=2))

k8s_resource(
    'pipecat-daily-bot-stg',
    new_name='bot-gateway',
    port_forwards='4444:4444',
    resource_deps=['redis']
)

k8s_resource(
    'pipecat-daily-bot-operator',
    new_name='bot-operator',
    objects=[
        'pipecat-bot-operator-sa',
        'pipecat-bot-operator-role',
        'pipecat-bot-operator-binding'
    ],
    resource_deps=['redis', 'bot-gateway', 'mesh']
)


k8s_resource(
    'bot-warm-pool',
    resource_deps=['redis', 'mesh']
)

# --- 4. Mesh ---
docker_build(
    'nia-mesh',
    '.',
    dockerfile='apps/mesh/Dockerfile',
    build_args={
        'NEXT_PUBLIC_API_URL': 'http://localhost:3000'
    },
    only=[
        'apps/mesh',
        'packages/prism',
        'package.json',
        'package-lock.json',
        '.npmrc',
        'tsconfig.json',
        'scripts'
    ],
    ignore=[
      '.git',
      '.codacy',
      '.codacy/**',
        'apps/mesh/.turbo',
        'apps/mesh/dist',
        'apps/mesh/.next',
        'apps/mesh/deployment',
        '**/node_modules',
        'packages/prism/.turbo',
        'packages/prism/dist',
        'packages/features/scripts'
    ]
)

k8s_yaml(load_patched_yaml('apps/mesh/deployment/staging/01-deployment.yaml', 'nia-mesh'))
k8s_yaml(load_patched_yaml('apps/mesh/deployment/staging/02-service.yaml', 'nia-mesh'))

k8s_resource(
    'mesh-stg',
    new_name='mesh',
    port_forwards=['2000:2000'],
    resource_deps=['postgres-testdb']
)

# --- OMITTED: Interface, Dashboard, Chorus TTS ---
